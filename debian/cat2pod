#!/bin/sed -f
s,^\x1b\[1m\(.*\)\x1b\[m,=head1 \1\n,g
/^=head1/ {
    x
    p
    s,.*,,
    x
}
s,\x1b\[1m\([^\x1b]*\)\x1b\[m,B<\1>,g
s,\x1b\[4m\([^\x1b]*\)\x1b\[m,I<\1>,g
s,^      ,,
s,^    ,,
/^B<[^>]*>/ {
N
    /^B<[^>]*>.*\n          .*$/ {
        x
        p
        s,.*,,
        x
        P
        x
        s,.*,=back\n,
        x
    i\
\n=over\n
        D
    }
    x
    p
    s,.*,,
    x
    P
    D
}
/^I<--/ {
    s/^I<--/=item I<--/
    i\
\n=over\n
    a\
\n=back\n
}

/^$/,/^    [A-Z]/ {
    /^    [A-Z]/ {
        i\
\n=over\n
    }
}

/^    [A-Z]/,/^$/ {
    /^$/ {
        a\
=back\n
    }
    s/^    //
}

/^$/,/^   [A-Z]/ {
    /^   [A-Z]/ {
        i\
\n=over\n
    }
}

/^   [A-Z]/,/^$/ {
    /^$/ {
        a\
=back\n
    }
    s/^   //
}
